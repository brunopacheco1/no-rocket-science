       IDENTIFICATION DIVISION.
       PROGRAM-ID. MISSION-000.
       AUTHOR. BRUNO PACHECO.
      *******************************************
      *                MISSION-000              *
      *******************************************

       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT MISSION-FILE ASSIGN TO "mission-000.dat"
            ORGANIZATION IS LINE SEQUENTIAL.
       
       DATA DIVISION.
       FILE SECTION.
       FD  MISSION-FILE.
       01  MESSAGE-DETAILS.
           88 END-OF-FILE VALUE HIGH-VALUES.
           02 MESSAGE-TEXT PIC X(64).

       WORKING-STORAGE SECTION.
       01  WS-WORK-AREAS.
           88 IS-MESSAGE VALUE HIGH-VALUES.
           05 PORT-NUMBER PIC 9(5).
           05 ODD-COUNTER PIC 99.
           05 I PIC 99.
           05 J PIC 99.
           05 K PIC 99.
           05 L PIC 99.
           05 PARITY-STEPS PIC 99.
           05 PARITY-CHUNK PIC 99.
           05 ERROR-COUNTER PIC 9.
       01  REVERSED-MESSAGE PIC X(64).
       01  PARITY-BIT OCCURS 7 TIMES.
           05 PARITY-INDEX PIC 99.
           05 PARITY-VALUE PIC 9.
           05 RECALC-PARITY-VALUE PIC 9.

       PROCEDURE DIVISION.
       
       0000-START.
           PERFORM 0001-READ-FILE.
           STOP RUN.

       0001-READ-FILE.
           DISPLAY "Reading mission-000.dat...".
           OPEN INPUT MISSION-FILE.
           READ MISSION-FILE.
           PERFORM 0002-READ-LINE UNTIL END-OF-FILE.
           CLOSE MISSION-FILE.
           DISPLAY "Finished.".

       0002-READ-LINE.
           IF IS-MESSAGE THEN
            PERFORM 0004-PROCESS-MESSAGE
           ELSE
            PERFORM 0003-PROCESS-PORT
           END-IF.
           SET IS-MESSAGE TO TRUE.
           READ MISSION-FILE
            AT END SET END-OF-FILE TO TRUE
            END-READ.

       0003-PROCESS-PORT.
           MOVE MESSAGE-TEXT(7:5) TO PORT-NUMBER.

       0004-PROCESS-MESSAGE.
           PERFORM 0005-REVERSE-MESSAGE.
           PERFORM 0006-READ-PARITY-BITS.
           PERFORM 0007-RECALCULATE-OVERALL-PARITY-BIT.
           PERFORM 0008-RECALCULATE-PARITY-BITS.
           PERFORM 0009-DETECT-ERROR.
       
       0005-REVERSE-MESSAGE.
           SET I TO 1.
           PERFORM VARYING J FROM 64 by -1 until J EQUAL 0
            MOVE MESSAGE-TEXT(I:1) to REVERSED-MESSAGE(J:1)
            ADD 1 TO I
           END-PERFORM.

       0006-READ-PARITY-BITS.
           SET PARITY-INDEX(1) TO 1
           MOVE REVERSED-MESSAGE(1:1) TO PARITY-VALUE(1)
           PERFORM VARYING I FROM 2 BY 1 UNTIL I IS GREATER 7
            COMPUTE PARITY-INDEX(I) = 2 ** (I - 2)
            ADD 1 TO PARITY-INDEX(I)
            MOVE REVERSED-MESSAGE(PARITY-INDEX(I):1) TO PARITY-VALUE(I)
           END-PERFORM.
        
       0007-RECALCULATE-OVERALL-PARITY-BIT.
           MOVE "0" TO REVERSED-MESSAGE(1:1)
           SET ODD-COUNTER TO 0.
           PERFORM VARYING I FROM 1 BY 1 UNTIL I IS GREATER 64
            IF REVERSED-MESSAGE(I:1) EQUAL "1" THEN
             ADD 1 TO ODD-COUNTER
            END-IF
           END-PERFORM.
           SET RECALC-PARITY-VALUE(1) TO FUNCTION MOD (ODD-COUNTER, 2).
       
       0008-RECALCULATE-PARITY-BITS.
           PERFORM VARYING I FROM 2 BY 1 UNTIL I IS GREATER 7
            MOVE "0" TO REVERSED-MESSAGE(PARITY-INDEX(I):1)
           END-PERFORM.
           PERFORM VARYING I FROM 2 BY 1 UNTIL I IS GREATER 7
            SET ODD-COUNTER TO 0
            COMPUTE PARITY-STEPS = 2 ** (I - 1)
            COMPUTE PARITY-CHUNK = 2 ** (I - 2)
            PERFORM VARYING J FROM PARITY-INDEX(I) BY PARITY-STEPS 
             UNTIL J IS GREATER 64
             PERFORM VARYING K FROM 0 BY 1 UNTIL K IS 
              EQUAL TO PARITY-CHUNK
              COMPUTE L = J + K
              IF REVERSED-MESSAGE(L:1) EQUAL "1" THEN
               ADD 1 TO ODD-COUNTER
              END-IF
             END-PERFORM
            END-PERFORM
            SET RECALC-PARITY-VALUE(I) TO FUNCTION MOD (ODD-COUNTER, 2)
           END-PERFORM.

       0009-DETECT-ERROR.
           PERFORM VARYING I FROM 1 BY 1 UNTIL I IS GREATER 7
            IF RECALC-PARITY-VALUE(I) IS NOT EQUAL TO PARITY-VALUE(I) 
             THEN
             ADD 1 TO ERROR-COUNTER
            END-IF
           END-PERFORM.
           DISPLAY "Errors detected: " ERROR-COUNTER.

       END PROGRAM MISSION-000.
